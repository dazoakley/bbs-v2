#! /usr/bin/env perl

use Mojolicious::Lite;
use GD::Graph::bars;
use GD::Graph::lines;

get '/' => sub {
  my $self = shift;
  $self->render('index');
};

get '/graph/:type' => sub {
  my $self = shift;
  my $type = $self->param('type');
  $self->res->headers->content_type('image/png');
  $self->res->body(draw_graph($type));
  $self->rendered(200);
};

sub draw_graph {
  my ($type) = @_;

  my @data = (
    ["1st","2nd","3rd","4th","5th","6th","7th", "8th", "9th"], # fields
    [    1,    2,    5,    6,    3,  1.5,    1,     3,     4], # dataset 1
    [    1,    1,    4,    7,    2,    3,    7,     4,     6]  # dataset 2
  );

  # Declare our new GD::Graph image
  my $graph = gd_graph_obj($type);

  $graph->set(
    x_label           => 'X Label',
    y_label           => 'Y label',
    title             => 'A simple graph',
    y_max_value       => 8,
    y_tick_number     => 8,
    y_label_skip      => 2
  ) or die $graph->error;

  my $gd = $graph->plot(\@data) or die $graph->error;

  # return raw PNG binary data
  return $gd->png;
}

sub gd_graph_obj {
  my ($type) = @_;

  if ($type eq "bars") {
    return GD::Graph::bars->new(400,300);
  } else {
    return GD::Graph::lines->new(400,300);
  }
}

app->start;
__DATA__

@@ layouts/default.html.ep
<!DOCTYPE html>
<html>
  <head>
    <title><%= title %></title>
  </head>
  <body>
    <%= content %>
  </body>
</html>

@@ index.html.ep
% layout 'default';
% title 'Using GD::Graph in a Web App';
<h1>Using GD::Graph in a Web App</h1>
<p>Here are the resulting images generated by GD::Graph</p>
<img src="/graph/bars" alt="Bar Chart" />
<img src="/graph/lines" alt="Line Graph" />

