#! /usr/bin/env perl

use Mojolicious::Lite;
use Statistics::R;
use DateTime;

app->attr(r => sub {
  my $self = shift;
  my $R    = Statistics::R->new();
  return $R;
});

get '/' => sub {
  my $self = shift;

  # Build our R Script in a multi-line string
  my $timestamp = DateTime->now();
  my $img_name  = "r-image-$timestamp.png";

  my $rcmd = <<RCMD;
rangescale <- function(X) {
  Xmax <- apply(X, 2, max)
  Xscaled <- scale(X, scale=Xmax, center=FALSE)
  return(Xscaled)
}

X <- read.table("http://www.bixsolutions.net/profiles.csv", sep=",", header=TRUE)
Xscaled <- rangescale(X)
d <- dist(t(Xscaled), method="euclidean")
bitmap(file="$img_name", type="png256", width=6, height=6, res=96)
dendrogram <- hclust(t(d), method = "complete", members = NULL)
plot(dendrogram)
dev.off()
RCMD

  chdir('public/r-images');
  my $R = $self->app->r;
  $R->startR;
  $R->send($rcmd);
  $R->stopR;

  $self->stash(img_name => $img_name);
  $self->render('index');
};

app->start;
__DATA__

@@ layouts/default.html.ep
<!DOCTYPE html>
<html>
  <head>
    <title><%= title %></title>
  </head>
  <body>
    <%= content %>
  </body>
</html>

@@ index.html.ep
% layout 'default';
% title 'Using R in a Perl Web App';
<h1>Using R in a Perl Web App</h1>
<p>Here is the resulting image generated by R</p>
<img src="/r-images/<%= $img_name %>" alt="R and Perl" />

