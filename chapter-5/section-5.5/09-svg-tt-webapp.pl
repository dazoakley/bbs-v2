#! /usr/bin/env perl

use Mojolicious::Lite;
use SVG::TT::Graph::Bar;
use SVG::TT::Graph::Line;

get '/' => sub {
  my $self = shift;
  $self->render('index');
};

get '/graph/:type' => sub {
  my $self = shift;
  my $type = $self->param('type');
  $self->res->headers->content_type('image/svg+xml');
  $self->res->body(draw_graph($type));
  $self->rendered(200);
};

sub draw_graph {
  my ($type) = @_;

  my $fields = ["1st", "2nd", "3rd", "4th", "5th", "6th", "7th", "8th", "9th"];
  my $graph  = svg_tt_graph_obj($type, $fields);

  $graph->add_data({
    data  => [1, 2, 5, 6, 3, 1.5, 1, 3, 4],
    title => 'Dataset 1'
  });

  $graph->add_data({
    data  => [1, 1, 4, 7, 2, 3, 7, 4, 6],
    title => 'Dataset 2'
  });

  return $graph->burn();
}

sub svg_tt_graph_obj {
  my ($type, $fields) = @_;

  my $graph_options = {
    height           => '300',
    width            => '400',
    fields           => $fields,
    x_title          => 'X Label',
    show_x_title     => 1,
    y_label          => 'Y Label',
    show_y_title     => 1,
    scale_integers   => 1,
    stagger_y_labels => 2,
    show_graph_title => 1,
    graph_title      => 'A simple graph'
  };

  if ($type eq 'line') {
    return SVG::TT::Graph::Line->new($graph_options);
  } else {
    return SVG::TT::Graph::Bar->new($graph_options);
  }
}

app->start;
__DATA__

@@ layouts/default.html.ep
<!DOCTYPE html>
<html>
  <head>
    <title><%= title %></title>
  </head>
  <body>
    <%= content %>
  </body>
</html>

@@ index.html.ep
% layout 'default';
% title 'Using SVG::TT::Graph in a Web App';
<h1>Using SVG::TT::Graph in a Web App</h1>
<p>Here are the resulting images generated by SVG::TT::Graph</p>
<embed src="/graph/line" type="image/svg+xml" height="300" width="400" />
<embed src="/graph/area" type="image/svg+xml" height="300" width="400" />

