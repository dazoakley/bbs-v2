#! /usr/bin/env perl

use Mojolicious::Lite;
use Chart::Clicker;
use Chart::Clicker::Renderer::Area;

get '/' => sub {
  my $self = shift;
  $self->render('index');
};

get '/graph/:type' => sub {
  my $self = shift;
  my $type = $self->param('type');
  $self->res->headers->content_type('image/svg+xml');
  $self->res->body(draw_graph($type));
  $self->rendered(200);
};

sub draw_graph {
  my ($type) = @_;

  my $cc = Chart::Clicker->new(
    width  => 400,
    height => 300,
    format => 'svg'
  );

  # Add data to our chart.
  $cc->add_data('Set 1', [5.8, 5.0, 4.9, 4.8, 4.5, 4.2]);
  $cc->add_data('Set 2', [0.7, 1.1, 1.7, 2.5, 3.0, 4.5]);

  set_renderer($cc, $type);

  $cc->draw;

  # return raw PNG binary data
  return $cc->rendered_data;
}

sub set_renderer {
  my ($cc, $type) = @_;

  if ($type eq "area") {
    my $renderer = Chart::Clicker::Renderer::Area->new(
      opacity => 0.75
    );
    $cc->set_renderer($renderer);
  }
}

app->start;
__DATA__

@@ layouts/default.html.ep
<!DOCTYPE html>
<html>
  <head>
    <title><%= title %></title>
  </head>
  <body>
    <%= content %>
  </body>
</html>

@@ index.html.ep
% layout 'default';
% title 'Using Chart::Clicker in a Web App';
<h1>Using Chart::Clicker in a Web App</h1>
<p>Here are the resulting images generated by Chart::Clicker</p>
<embed src="/graph/line" type="image/svg+xml" height="300" width="400" />
<embed src="/graph/area" type="image/svg+xml" height="300" width="400" />

